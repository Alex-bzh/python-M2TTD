{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Transformations XSL : les fondamentaux"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Structure d’une feuille de transformation"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Squelette\n",
    "\n",
    "```xml\n",
    "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n",
    "\n",
    "<!-- root element -->\n",
    "<xsl:stylesheet version=\"3.0\" xmlns:xsl=\"http://www.w3.org/1999/XSL/Transform\">\n",
    "\n",
    "    <!-- document to be produced -->\n",
    "    <xsl:output method=\"xml\" version=\"1.0\" encoding=\"utf-8\" indent=\"yes\"/>\n",
    "\n",
    "    <!-- main rule -->\n",
    "    <xsl:template match=\"/\">\n",
    "        <!-- backbone -->\n",
    "    </xsl:template>\n",
    "\n",
    "</xsl:stylesheet>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Élément racine\n",
    "\n",
    "Son rôle est de spécifier la version du langage utilisé et de déclarer l’espace de nommage qui permet de qualifier les fonctions XSL. Par convention, il s’agit du préfixe `xsl`."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Fonction `output`\n",
    "\n",
    "Cette fonction décrit le document de sortie à l’aide des attributs suivants :\n",
    "- `method` pour le type de document (`xml` `html` `xhtml` `text` `json` `adaptive`)\n",
    "- `version` pour la version du format\n",
    "- `encoding` pour l’encodage utilisé\n",
    "- `indent` pour un document indenté ou non (`yes` `no`)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Rôles du template structurant\n",
    "\n",
    "Le template structurant a pour rôle premier d’établir la règle de transformation principale en plaçant le pointeur à la racine du document à transformer. C’est le sens de l’expression XPath `/` assignée comme valeur de l’attribut `match`. Ensuite, il décrit le squelette du document de sortie en appelant les règles de transformation secondaires soit par directement par leur nom (l’attribut `name` est alors obligatoire), soit par un appel à candidatures."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Le point sur les templates"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Définition\n",
    "\n",
    "**Template :** modèle de transformation appliqué systématiquement (sauf contrordre).\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"{XPath}\" name=\"monTpl\">\n",
    "    <!-- actions -->\n",
    "</xsl:template>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Fonction `template`\n",
    "\n",
    "La fonction `template` décrit les actions à exécuter lorsqu’une commande lui est transmise. Il prend deux arguments :\n",
    "- `match` avec une expression XPath pour sélectionner les nœuds impactés par la commande ;\n",
    "- `name` (facultatif) pour lui attribuer un nom.\n",
    "\n",
    "Un template ne produira de résultat que s’il peut répondre aux critères exigés par une commande. Considérons le code XML :\n",
    "\n",
    "```xml\n",
    "<characters>\n",
    "    <character>\n",
    "        <firstName>Sheldon</firstName>\n",
    "        <lastName>Cooper</lastName>\n",
    "    </character>\n",
    "    <character>\n",
    "        <firstName>Howard</firstName>\n",
    "        <lastName>Hofstadter</lastName>\n",
    "    </character>\n",
    "</characters>\n",
    "```\n",
    "\n",
    "Si une commande spéciale `/characters` est transmise au template ci-dessous, elle ne produira aucun résultat :\n",
    "\n",
    "```xml\n",
    "<template match=\"firstName\">\n",
    "    <xsl:value-of select=\".\"/>\n",
    "</template>\n",
    "```\n",
    "\n",
    "Dans cet exemple, la commande manque de précision. Le template aurait en revanche pu répondre à une commande du type `//character`.\n",
    "\n",
    "#### Création de nœuds\n",
    "\n",
    "Pour créer une structure XML dans un template, il suffit souvent de recourir à une syntaxe *in extenso* :\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"character\">\n",
    "    <b lastname=\"lastName\"><xsl:value-of select=\"firstName\"/></b>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "Appliqué au fragment XML décrit plus haut, ce template produira la structure suivante :\n",
    "\n",
    "```xml\n",
    "<b lastname=\"lastName\">Sheldon</b>\n",
    "<b lastname=\"lastName\">Leonard</b>\n",
    "```\n",
    "\n",
    "Le problème ici vient du fait que l’élément `b`, n’étant pas préfixé par `xsl`, n’a aucune fonction dans le langage et se comporte comme du texte brut. Le terme `lastName` assigné comme valeur de l’attribut `lastname` n’est par conséquent interprété comme une expression XPath.\n",
    "\n",
    "La parade consiste à soit utiliser des fonctions XSL pour créer les éléments et les attributs, soit à entourer l’expression par des accolades (`{lastName}`) dans la syntaxe *in extenso*.\n",
    "\n",
    "```xml\n",
    "<!-- curly braces to identify XPath expressions -->\n",
    "<xsl:template match=\"character\">\n",
    "    <b lastname=\"{lastName}\"><xsl:value-of select=\"firstName\"/></b>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "```xml\n",
    "<!-- XSL functions 'element' and 'attribute' -->\n",
    "<xsl:template match=\"character\">\n",
    "    <xsl:element name=\"b\">\n",
    "        <xsl:attribute name=\"lastname\">\n",
    "            <xsl:value-of select=\"lastName\"/>\n",
    "        </xsl:attribute>\n",
    "        <xsl:value-of select=\"firstName\"/>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Fonction `apply-templates`\n",
    "\n",
    "La fonction `apply-templates` est chargée d’effectuer un appel à candidatures aux templates décrits en définissant les critères de l’appel grâce à l’attribut `select` qui prend une expression XPath comme valeur.\n",
    "\n",
    "```xml\n",
    "<!-- main rule -->\n",
    "<xsl:template match=\"/\">\n",
    "    <!-- function 'element' to create '<bbt/>' -->\n",
    "    <xsl:element name=\"bbt\">\n",
    "        <!-- function 'attribut' to create an attribute 'creation' -->\n",
    "        <xsl:attribute name=\"creation\">2007</xsl:attribute>\n",
    "        <!-- call for application -->\n",
    "        <xsl:apply-templates select=\"//character\"/>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "Le template précédent donnera pour résultat :\n",
    "\n",
    "```xml\n",
    "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n",
    "<bbt creation=\"2007\">\n",
    "   <b lastName=\"Cooper\">Sheldon</b>\n",
    "   <b lastName=\"Hofstadter\">Leonard</b>\n",
    "</bbt>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Quelques opérations courantes"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Extraire l’information\n",
    "\n",
    "#### Fonction `value-of`\n",
    "\n",
    "Pour récupérer la valeur textuelle d’un nœud grâce à une expression XPath dans un attribut `select`.\n",
    "\n",
    "```xml\n",
    "<!-- print: Leonard -->\n",
    "<xsl:value-of select=\"character/firstName\"/>\n",
    "```\n",
    "\n",
    "#### Fonction `copy-of`\n",
    "\n",
    "Copie en profondeur pour récupérer l’arborescence d’un nœud grâce à une expression XPath dans un attribut `select`.\n",
    "\n",
    "```xml\n",
    "<!-- print: <firstName>Leonard</firstName> -->\n",
    "<xsl:copy-of select=\"character/firstName\"/>\n",
    "```\n",
    "\n",
    "#### Fonction `text`\n",
    "\n",
    "Pour insérer du texte.\n",
    "\n",
    "```xml\n",
    "<!-- a whitespace character -->\n",
    "<xsl:text> </xsl:text>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Automatiser des instructions\n",
    "\n",
    "#### Fonction `for-each`\n",
    "\n",
    "Pour répéter des actions sur chaque nœud renvoyé par une expression XPath validé dans un attribut `select`.\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"/\">\n",
    "    <xsl:element name=\"bbt\">\n",
    "        <xsl:attribute name=\"creation\">2007</xsl:attribute>\n",
    "        <xsl:for-each select=\"//character\">\n",
    "            <!-- print: <lastName>Hofstadter</lastName> -->\n",
    "            <xsl:copy-of select=\"lastName\"/>\n",
    "        </xsl:for-each>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "#### Fonction `call-template`\n",
    "\n",
    "Appel explicite à un template par son nom. L’attribut `name` prend comme valeur le nom d’un template et non pas une expression XPath.\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"/\">\n",
    "    <xsl:element name=\"bbt\">\n",
    "        <xsl:attribute name=\"creation\">2007</xsl:attribute>\n",
    "        <xsl:for-each select=\"//character\">\n",
    "            <!-- call to a specific template 'Character' -->\n",
    "            <xsl:call-template name=\"Character\"/>\n",
    "        </xsl:for-each>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Trier des données\n",
    "\n",
    "#### Fonction `sort`\n",
    "\n",
    "Pour trier un ensemble de nœuds grâce à une expression XPath dans un attribut `select` qui désigne la clé de tri. Divers attributs permettent de gérer les paramètres.\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"/\">\n",
    "    <xsl:element name=\"bbt\">\n",
    "        <xsl:attribute name=\"creation\">2007</xsl:attribute>\n",
    "        <xsl:for-each select=\"//character\">\n",
    "            <!-- sorting by lastName -->\n",
    "            <xsl:sort select=\"lastName\"/>\n",
    "            <xsl:call-template name=\"Character\"/>\n",
    "        </xsl:for-each>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "|Attribut|Description|Valeurs autorisées|\n",
    "|:-:|:-|:-:|\n",
    "|`order`|Tri croissant ou décroissant|ascending descending|\n",
    "|`case-order`|Priorité aux minuscules ou aux majuscules (par défaut)|`upper-first` `lower-first`|\n",
    "|`data-type`|Type des données à trier : nombres ou texte (par défaut)|`text` `number`|\n",
    "|`lang`|Code langue pour le tri lexicographique|`fr` `de` `en` `ja` …|"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Tests\n",
    "\n",
    "#### Fonction `if`\n",
    "\n",
    "Pour émettre une condition de transformation grâce à une expression de comparaison dans un attribut `test`.\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"character\" name=\"Character\">\n",
    "    <xsl:element name=\"personnage\">\n",
    "        <xsl:attribute name=\"identite\">\n",
    "            <xsl:value-of select=\"firstName\"/>\n",
    "            <!-- if <firstName> exists -->\n",
    "            <!-- as charcater Penny doesn't have a known lastName, the whitespace is useless -->\n",
    "            <xsl:if test=\"lastName\">\n",
    "                <xsl:text> </xsl:text>\n",
    "                <xsl:value-of select=\"lastName\"/>\n",
    "            </xsl:if>\n",
    "        </xsl:attribute>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "|Expression|Description|\n",
    "|:-:|:-|\n",
    "|`element`|Teste la présence d’un élément `element`|\n",
    "|`@attribut`|Teste la présence d’un attribut `attribut`|\n",
    "|`a = b`|Teste si `a` et `b` partagent la même valeur.|\n",
    "|`not(a = b)`|Teste si `a` et `b` ne partagent pas la même valeur.|\n",
    "|`a &lt; b`|Teste si la valeur de `a` est strictement inférieure à `b`.|\n",
    "|`a &gt; b`|Teste si la valeur de `a` est strictement supérieure à `b`.|\n",
    "|`a &lt;= b`|Teste si la valeur de `a` est inférieure ou égale à `b`.|\n",
    "|`a &gt;= b`|Teste si la valeur de `a` est supérieure ou égale à `b`.|\n",
    "\n",
    "**Remarques :**\n",
    "- les entités doivent vraiment être encodées telles quelles ;\n",
    "- les opérateurs logiques `and` et `or` permettent de combiner plusieurs conditions.\n",
    "\n",
    "#### Fonction `choose`\n",
    "\n",
    "Pour soumettre une transformation à des conditions successives, en combinaison des fonctions `when` et `otherwise`, la première permettant d’exprimer *x* conditions, la seconde une opération par défaut.\n",
    "\n",
    "```xml\n",
    "<!-- XML document -->\n",
    "<americanDad>\n",
    "    <character name=\"Francine\" gender=\"F\"/>\n",
    "    <character name=\"Stan\" gender=\"M\"/>\n",
    "    <character name=\"Roger\"/>\n",
    "</americanDad>\n",
    "```\n",
    "\n",
    "```xml\n",
    "<!-- XSL template -->\n",
    "<xsl:template match=\"//character\">\n",
    "    <xsl:choose>\n",
    "        <xsl:when test=\"@gender = 'M'\">\n",
    "            <xsl:text>M. </xsl:text>\n",
    "        </xsl:when>\n",
    "        <xsl:when test=\"@gender = 'F'\">\n",
    "            <xsl:text>Mme. </xsl:text>\n",
    "        </xsl:when>\n",
    "        <xsl:otherwise>\n",
    "            <xsl:text>? </xsl:text>\n",
    "        </xsl:otherwise>\n",
    "    </xsl:choose>\n",
    "    <xsl:value-of select=\"@name\"/>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "```txt\n",
    "<!-- print -->\n",
    "Mme Francine\n",
    "M. Stan\n",
    "? Roger\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Le point sur les variables"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Document de référence\n",
    "\n",
    "```xml\n",
    "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n",
    "<show title=\"The Big Bang Theory\">\n",
    "    <characters>\n",
    "        <character pID=\"sheldon\">\n",
    "            <firstName>Sheldon</firstName>\n",
    "            <lastName>Cooper</lastName>\n",
    "        </character>\n",
    "        <character pID=\"penny\">\n",
    "            <firstName>Penny</firstName>\n",
    "        </character>\n",
    "    </characters>\n",
    "    <actors>\n",
    "        <actor pIDREF=\"penny\">\n",
    "            <firstName>Kaley</firstName>\n",
    "            <lastName>Cuoco-Sweeting</lastName>\n",
    "            <birth>1985-11-30</birth>\n",
    "        </actor>\n",
    "        <actor pIDREF=\"sheldon\">\n",
    "            <firstName>Jim</firstName>\n",
    "            <lastName>Parsons</lastName>\n",
    "            <birth>1973-03-24</birth>\n",
    "        </actor>\n",
    "    </actors>\n",
    "</show>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Fonction `variable`\n",
    "\n",
    "Pour stocker de l’information en mémoire, sélectionnée grâce à une expression XPath dans un attribut `select`. L’attribut `name` lui fournit un nom, appelé plus loin en le préfixant du signe `$`.\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"character\" name=\"Character\">\n",
    "    <!-- 'pID' attribute stored into a variable named 'id' -->\n",
    "    <xsl:variable name=\"id\" select=\"@pID\"/>\n",
    "    <xsl:element name=\"character\">\n",
    "        <xsl:element name=\"id\">\n",
    "            <!-- reference to variable '$id' -->\n",
    "            <xsl:value-of select=\"$id\"/>\n",
    "        </xsl:element>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```\n",
    "\n",
    "Il est possible de stocker une valeur brute dans une variable :\n",
    "\n",
    "```xml\n",
    "<xsl:variable name=\"show\">The Big Bang Theory</xsl:variable>\n",
    "```\n",
    "\n",
    "#### Exemple\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"/\">\n",
    "    <bbt creation=\"2007\">\n",
    "        <!-- call for application -->\n",
    "        <xsl:apply-templates select=\"//characters\"/>\n",
    "    </bbt>\n",
    "</xsl:template>\n",
    "\n",
    "<xsl:template match=\"character\">\n",
    "    <!-- store 'pID' value -->\n",
    "    <xsl:variable name=\"id\" select=\"@pID\"/>\n",
    "    <!-- store details about actor -->\n",
    "    <xsl:variable name=\"actor\" select=\"//actor[@pIDREF = $id]\"/>\n",
    "    <xsl:element name=\"character\">\n",
    "        <xsl:attribute name=\"name\">\n",
    "            <xsl:value-of select=\"firstName\"/>\n",
    "            <xsl:text> </xsl:text>\n",
    "            <xsl:value-of select=\"lastName\"/>\n",
    "        </xsl:attribute>\n",
    "        <!-- details about the actor in an attribute -->\n",
    "        <xsl:attribute name=\"actor\">\n",
    "            <xsl:value-of select=\"$actor/firstName\"/>\n",
    "            <xsl:text> </xsl:text>\n",
    "            <xsl:value-of select=\"$actor/lastName\"/>\n",
    "        </xsl:attribute>\n",
    "    </xsl:element>\n",
    "</xsl:template>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Les paramètres"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Fonction `with-param`\n",
    "\n",
    "À l’intérieur d’une fonction `call-template`, ce mécanisme permet de transmettre un paramètre au template. L’attribut `name` fait référence au paramètre et l’attribut `select` en fixe la valeur grâce à une expression XPath.\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"/\">\n",
    "    <bbt creation=\"2007\">\n",
    "        <xsl:for-each select=\"//character\">\n",
    "            <xsl:call-template name=\"Character\">\n",
    "                <!-- firstName is transmitted to the template -->\n",
    "                <xsl:with-param name=\"printFirstName\" select=\"firstName\"/>\n",
    "            </xsl:call-template>\n",
    "        </xsl:for-each>\n",
    "    </bbt>\n",
    "</xsl:template>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Fonction `param`\n",
    "\n",
    "À l’intérieur d’une fonction template, ce mécanisme permet de déclarer un paramètre pour le template appelé. L’attribut `name` permet de le nommer.\n",
    "\n",
    "```xml\n",
    "<xsl:template match=\"character\" name=\"Character\">\n",
    "    <!-- declaration -->\n",
    "    <xsl:param name=\"printFirstName\"/>\n",
    "    <xsl:param name=\"labelFirstName\">FirstName :</xsl:param>\n",
    "    <!-- print -->\n",
    "    <character>\n",
    "        <xsl:value-of select=\"$labelFirstName\"/>\n",
    "        <xsl:text> </xsl:text>\n",
    "        <xsl:value-of select=\"$printFirstName\"/>\n",
    "    </character>\n",
    "</xsl:template>\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.13"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
