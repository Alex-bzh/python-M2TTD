{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Quelques techniques courantes"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Insertion de contenu textuel\n",
    "\n",
    "La fonction XSL `text` permet d’insérer facilement tout caractère textuel.\n",
    "\n",
    "### Insérer du texte brut\n",
    "\n",
    "```xml\n",
    "<!-- plain text -->\n",
    "<xsl:text>Nom :</xsl:text>\n",
    "\n",
    "<!-- a whitespace -->\n",
    "<xsl:text> </xsl:text>\n",
    "```\n",
    "\n",
    "### Insérer une balise autonome\n",
    "\n",
    "```xml\n",
    "<!-- special characters are printed as is -->\n",
    "<xsl:text disable-output-escaping=\"yes\">&amp;lt;br/&amp;gt;</xsl:text>\n",
    "```\n",
    "\n",
    "### Insérer un caractère ASCII\n",
    "\n",
    "```xml\n",
    "<!-- hexadécimal notation (backspace) -->\n",
    "<xsl:text>&amp;#xA;</xsl:text>\n",
    "<!-- decimal notation (backspace) -->\n",
    "<xsl:text>&amp;#10;</xsl:text>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Le recours aux variables\n",
    "\n",
    "### Stocker un motif récurrent\n",
    "\n",
    "```xml\n",
    "<!-- delimiter into a variable -->\n",
    "<xsl:variable name=\"delimiter\" select=\"','\"/>\n",
    "\n",
    "<!-- call to the variable -->\n",
    "<xsl:value-of select=\"data-1\"/>\n",
    "<xsl:value-of select=\"$delimiter\"/>\n",
    "<xsl:value-of select=\"data-2\"/>\n",
    "<xsl:value-of select=\"$delimiter\"/>\n",
    "<xsl:value-of select=\"data-3\"/>\n",
    "```\n",
    "\n",
    "### Stocker le résultat d’une expression XPath complexe\n",
    "\n",
    "```xml\n",
    "<!-- store into variable named 'carina' -->\n",
    "<xsl:variable name=\"carina\" select=\"//constellation[name = 'Carina']\"/>\n",
    "\n",
    "<!-- calls to the variable 'carina' -->\n",
    "<xsl:value-of select=\"$carina/name/@fr\"/>\n",
    "<xsl:value-of select=\"$carina//area[@unit = 'deg2']\"/>\n",
    "\n",
    "<!-- without calling the variable -->\n",
    "<xsl:value-of select=\"//constellation[name = 'Carina']name/@fr\"/>\n",
    "<xsl:value-of select=\"//constellation[name = 'Carina']//area[@unit = 'deg2']\"/>\n",
    "```\n",
    "\n",
    "**Remarque :** recourir aux variables est plus rapide que d’évaluer des expressions XPath complètes.\n",
    "\n",
    "```xml\n",
    "<!-- variables -->\n",
    "<xsl:variable name=\"c1\" select=\"//constellation[1]\"/>\n",
    "<xsl:variable name=\"c2\" select=\"//constellation[2]\"/>\n",
    "\n",
    "<!-- call to variables -->\n",
    "<table>\n",
    "    <tr>\n",
    "        <th><xsl:value-of select=\"$c1/name\"/></th>\n",
    "        <th><xsl:value-of select=\"$c2/name\"/></th>\n",
    "    </tr>\n",
    "    <tr>\n",
    "        <td><xsl:value-of select=\"$c1//area[@unit = 'percent']\"/></td>\n",
    "        <td><xsl:value-of select=\"$c2//area[@unit = 'percent']\"/></td>\n",
    "    </tr>\n",
    "    <tr>\n",
    "        <td><xsl:value-of select=\"$c1//area[@unit = 'deg2']\"/></td>\n",
    "        <td><xsl:value-of select=\"$c2//area[@unit = 'deg2']\"/></td>\n",
    "    </tr>\n",
    "</table>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Évaluer des expressions XPath\n",
    "\n",
    "### Éléments XSL et non-XSL\n",
    "\n",
    "Le préfixe `xsl` identifie un élément dans l’espace de nommage qui fait référence à la grammaire XSL :\n",
    "\n",
    "```xml\n",
    "<xsl:stylesheet xmlns:xsl=\"http://www.w3.org/1999/XSL/Transform\" version=\"3.0\">\n",
    "    <!-- transformations -->\n",
    "</xsl:stylesheet>\n",
    "```\n",
    "\n",
    "À partir de ce moment, les éléments préfixés `xsl` ont une fonction bien définie :\n",
    "\n",
    "```xml\n",
    "<!-- XSL element 'output' -->\n",
    "<xsl:output/>\n",
    "\n",
    "<!-- undefined element 'output' -->\n",
    "<output/>\n",
    "```\n",
    "\n",
    "Les éléments XSL sont prévus pour évaluer des expressions XPath :\n",
    "\n",
    "```xml\n",
    "<xsl:value-of select=\"//constellation[4]/name\"/>\n",
    "```\n",
    "\n",
    "En revanche, évaluer une expression XPath dans un élément non-XSL se fait en l’encadrant d’accolades :\n",
    "\n",
    "```xml\n",
    "<!-- curly braces allow XPath expressions -->\n",
    "<areas constellation=\"{//constellation[4]/name}\"/>\n",
    "```\n",
    "\n",
    "### Court-circuitage avec la fonction `choose`\n",
    "\n",
    "La structure `choose` est idéale pour éliminer plusieurs conditions simples :\n",
    "\n",
    "```xml\n",
    "<xsl:variable name=\"puc\" select=\"//Equipe[@NomClub = 'Paris']\"/>\n",
    "<xsl:variable name=\"lyon\" select=\"//Equipe[@NomClub = 'Lyon']\"/>\n",
    "<xsl:variable name=\"nancy\" select=\"//Equipe[@NomClub = 'Nancy']\"/>\n",
    "<xsl:choose>\n",
    "    <xsl:when test=\"$puc/number(Points) > $lyon/number(Points)\"/>\n",
    "    <xsl:when test=\"$puc/number(Points) > $nancy/number(Points)\"/>\n",
    "    <xsl:otherwise>\n",
    "        <xsl:call-template name=\"alerteCoach\"/>\n",
    "    </xsl:otherwise>\n",
    "</xsl:choose>\n",
    "```\n",
    "\n",
    "Elle est préférable à une expression XPath complexe :\n",
    "\n",
    "```xml\n",
    "<xsl:if test=\"(//Equipe[@NomClub = 'Paris']/number(Points) &lt; //Equipe[@NomClub = 'Nancy']/number(Points) and //Equipe[@NomClub = 'Paris']/number(Points) &lt; //Equipe[@NomClub = 'Lyon']/number(Points))\">\n",
    "    <xsl:call-template name=\"alerteCoach\"/>\n",
    "</xsl:if>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Les structures itératives\n",
    "\n",
    "La fonction `for-each` permet d’appliquer un traitement identique à chaque élément d’un ensemble de nœuds :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation\">\n",
    "    <tr>\n",
    "        <td><xsl:value-of select=\"@origin\"/></td>\n",
    "        <td><xsl:value-of select=\"name\"/></td>\n",
    "    </tr>\n",
    "</xsl:for-each>\n",
    "```\n",
    "\n",
    "Elle accepte également la fonction `sort` pour le tri des données.\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation\">\n",
    "    <xsl:sort select=\"areas/area[@unit = 'deg2']\" data-type=\"number\" order=\"descending\"/>\n",
    "    <tr>\n",
    "        <td><xsl:value-of select=\"@origin\"/></td>\n",
    "        <td><xsl:value-of select=\"name\"/></td>\n",
    "    </tr>\n",
    "</xsl:for-each>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Générer des identifiants uniques\n",
    "\n",
    "Chaque élément dispose d’un identifiant unique que la fonction XPath `generate-id()` révèle :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation\">\n",
    "    <xsl:element name=\"a\">\n",
    "        <xsl:attribute name=\"href\">\n",
    "            <xsl:value-of select=\"generate-id()\"/>\n",
    "        </xsl:attribute>\n",
    "        <xsl:value-of select=\"name\"/>\n",
    "    </xsl:element>\n",
    "</xsl:for-each>\n",
    "```\n",
    "\n",
    "Syntaxe équivalente à :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation\">\n",
    "    <a href=\"#{generate-id()}\"><xsl:value-of select=\"name\"/></a>\n",
    "</xsl:for-each>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Regrouper des données\n",
    "\n",
    "L’idée est de regrouper des données partageant une même clé sous une seule entrée. Par exemple, dans le fichier sur les [constellations](./files/xml/constellations.xml), plusieurs d’entre elles ont été découvertes par Ptolémée et d’autres par Lacaille.\n",
    "\n",
    "L’objectif ici est de créer un nouveau fichier XML en regroupant les constellations par le nom de leur découvreur, dans une liste triée alphabétiquement :\n",
    "\n",
    "```xml\n",
    "<astronoms>\n",
    "    <astronom name=\"Lacaille\">\n",
    "        <constellation>Carina</constellation>\n",
    "        <constellation>Microscopium</constellation>\n",
    "        …\n",
    "    </astronom>\n",
    "    <astronom name=\"Ptolémée\">\n",
    "        <constellation>Aquila</constellation>\n",
    "        <constellation>Auriga</constellation>\n",
    "        …\n",
    "    </astronom>\n",
    "</astronoms>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### La méthode muenchienne\n",
    "\n",
    "Méthode ficelée par Steve Muench pour filtrer les doublons. Elle repose sur les fonctions XSL `key` et XPath `generate-id()`.\n",
    "\n",
    "La première étape consiste à créer un index des entrées à dédoublonner avec la fonction `key`, à déclarer comme enfant de l’élément racine `stylesheet` :\n",
    "\n",
    "```xml\n",
    "<!-- astronoms -->\n",
    "<xsl:key name=\"astronoms\" match=\"constellation\" use=\"@origin\"/>\n",
    "```\n",
    "\n",
    "Ensuite, une fois la clé définie, la fonction `key` permet de sélectionner selon un paramètre :\n",
    "\n",
    "```xml\n",
    "<!-- each time the astronom is 'Lacaille' -->\n",
    "<xsl:for-each select=\"key('astronoms', 'Lacaille')\">\n",
    "    <!-- LacailleLacailleLacaille -->\n",
    "    <astronom><xsl:value-of select=\"@origin\"/></astronom>\n",
    "</xsl:for-each>\n",
    "```\n",
    "\n",
    "Les données étant redondantes, il suffit de sélectionner la première occurrence :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"key('astronoms', 'Lacaille')[1]\">\n",
    "    <!-- Lacaille -->\n",
    "    <astronom><xsl:value-of select=\"@origin\"/></astronom>\n",
    "</xsl:for-each>\n",
    "```\n",
    "\n",
    "La dernière étape consiste à envoyer dynamiquement le nom de tous les découvreurs, nom qui est disponible dans l’attribut `@origin` :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation[key('astronoms', @origin)[1]]\">\n",
    "    <astronom><xsl:value-of select=\"@origin\"/></astronom>\n",
    "</xsl:for-each>\n",
    "```\n",
    "\n",
    "Le problème de cette instruction est qu’elle va afficher, pour chaque constellation, le nom de son découvreur. Le même résultat aurait pu être obtenu avec l’expression XPath `//constellation`. Pour dédoublonner les entrées, l’astuce réside dans la comparaison des identifiants uniques de chaque constellation :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation[generate-id() = generate-id(key('astronoms', @origin)[1])]\">\n",
    "    <!--\n",
    "        <astronom>Lacaille</astronom>\n",
    "        <astronom>Ptolémée</astronom>\n",
    "    -->\n",
    "    <astronom><xsl:value-of select=\"@origin\"/></astronom>\n",
    "</xsl:for-each>\n",
    "```\n",
    "\n",
    "Dans le même ordre d’idée, le même résultat est obtenu en vérifiant que le nœud courant et le 1er élément des nœuds sélectionnés avec `key()` ne font qu’un :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation[count( . | key('astronoms', @origin)[1]) = 1]\">\n",
    "    <astronom><xsl:value-of select=\"@origin\"/></astronom>\n",
    "</xsl:for-each>\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Le filtrage systématique\n",
    "\n",
    "Bien plus facile à appréhender mais gourmande en ressources, cette technique exploite le lignage `preceding` pour sélectionner tous les nœuds qui précèdent (à l’exception des ancêtres, des attributs et des espaces de nommage).\n",
    "\n",
    "Le nom du découvreur n’est retenu que s’il ne figure pas déjà dans ceux retenus précédemment :\n",
    "\n",
    "```xml\n",
    "<xsl:for-each select=\"//constellation[not(preceding::constellation/@origin = @origin)]\">\n",
    "    <astronom><xsl:value-of select=\"@origin\"/></astronom>\n",
    "</xsl:for-each>\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
